#!/usr/local/bin/fish
#
# Opens a new iTerm tab and go in the appropriate
# directory intelligently.
#
# Takes directory search query as argument.
# If no argument is passed, a new tab is opened
# in the same directory.
#
# Else if:
#
# - argument is a valid directory path relative
#   to the home directory:
#     => cd into that directory
# - argument is a valid fish function or command:
#     => execute that function/command
# - argument is none of the above:
#     => cd to the first autojump match
#

set -l cmd

if test (count $argv) -eq 0
  set -l directory (pwd)
  set cmd " cd $directory"
else
  if test -d $argv[1]
    set cmd " cd $argv[1]"
  else if test -d "$HOME/$argv[1]"
    set cmd " cd $HOME/$argv[1]"
  else if contains $argv[1] (functions)
    set cmd " $argv[1]"
  else
    set cmd " cd (autojump $argv[1])"
  end
end

set -U NEW_SESSION_CMD $cmd

osascript -e "tell application \"iTerm\"" \
          -e "tell the current terminal to launch session \"Railscasts\"" \
          -e "end tell" > /dev/null ^/dev/null

history --delete -contains "tab"
history --delete -prefix "tab"
